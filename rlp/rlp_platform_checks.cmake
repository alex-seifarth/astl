include(CheckIncludeFiles)
include(CheckPrototypeDefinition)

function(rlp_setup_mechanism)
    check_include_files("sys/epoll.h" has_epoll)
    check_include_files("sys/eventfd.h" has_eventfd)
    check_prototype_definition(kqueue "int kqueue()" 0 "sys/event.h" has_kqueue)
    check_include_files("sys/poll.h" has_poll)

    if(NOT ASTL_RLP_AIO_MECHANISM)
        if(has_epoll AND has_eventfd)
            set(ASTL_RLP_AIO_MECHANISM EPOLL)
        elseif(has_kqueue)
            set(ASTL_RLP_AIO_MECHANISM KQUEUE)
        endif()
        message(STATUS "Determined ASTL_RLP_AIO_MECHANISM: ${ASTL_RLP_AIO_MECHANISM}")
    endif()
    set(ASTL_RLP_AIO_MECHANISM ${ASTL_RLP_AIO_MECHANISM} CACHE STRING "Choose the asynchronous IO mechanism for ASTL event runloop")
    set_property(CACHE ASTL_RLP_AIO_MECHANISM PROPERTY STRINGS EPOLL POLL KQUEUE IOURING)

    #do checks for the selected mechanism
    if("${ASTL_RLP_AIO_MECHANISM}" STREQUAL "EPOLL")
        if(NOT (has_epoll AND has_eventfd))
            message(FATAL_ERROR "ASTL_RLP_AIO_MECHANISM = EPOLL, but sys/epoll.h and/or sys/eventfd.h not available")
        endif()
    elseif("${ASTL_RLP_AIO_MECHANISM}" STREQUAL "KQUEUE")
        if(NOT has_kqueue)
            message(FATAL_ERROR "ASTL_RLP_AIO_MECHANISM = KQUEUE, but sys/event.h and int kqueue() not available")
        endif()
    elseif("${ASTL_RLP_AIO_MECHANISM}" STREQUAL "POLL")
        message(FATAL_ERROR "ASTL_RLP_AIO_MECHANISM = POLL, actually not supported")
    elseif("${ASTL_RLP_AIO_MECHANISM}" STREQUAL "IOURING")
        message(FATAL_ERROR "ASTL_RLP_AIO_MECHANISM = IOURING, actually not supported")
    else()
        message(FATAL_ERROR "ASTL_RLP_AIO_MECHANISM = ${ASTL_RLP_AIO_MECHANISM} - undefined value. Possible values EPOLL|POLL|KQUEUE|IOURING.")
    endif()
    message(STATUS " * ASTL_RLP_AIO_MECHANISM ${ASTL_RLP_AIO_MECHANISM}")
endfunction()
